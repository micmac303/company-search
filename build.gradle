plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.trunarrative'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integrationTests/java')
		}
		resources.srcDir file('src/integrationTests/resources')
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	integrationTestImplementation sourceSets.main.output
	integrationTestImplementation sourceSets.test.output
}

tasks.register('integrationTest', Test) {
	description = 'Run the integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	shouldRunAfter test
}

tasks.named('integrationTest') {
	useJUnitPlatform()
}


check.dependsOn integrationTest
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.hsqldb:hsqldb'
	annotationProcessor 'org.projectlombok:lombok'
	integrationTestAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	integrationTestImplementation 'org.projectlombok:lombok'
	integrationTestImplementation 'io.cucumber:cucumber-java:7.18.0'
	integrationTestImplementation 'io.cucumber:cucumber-java8:7.18.0'
	integrationTestImplementation 'io.cucumber:cucumber-junit-platform-engine:7.18.0'
	integrationTestImplementation 'io.cucumber:cucumber-junit:7.18.0'
	integrationTestImplementation 'io.cucumber:cucumber-spring:7.18.0'
	integrationTestImplementation 'org.assertj:assertj-core:3.23.1'
	integrationTestImplementation 'org.awaitility:awaitility:4.2.0'
	integrationTestImplementation 'org.junit.jupiter:junit-jupiter'
	integrationTestImplementation 'org.junit.platform:junit-platform-suite'
	integrationTestImplementation 'org.junit.platform:junit-platform-suite:1.9.0'
	integrationTestImplementation 'org.skyscreamer:jsonassert:1.5.1'
	integrationTestImplementation("org.testcontainers:junit-jupiter")
	integrationTestImplementation("org.testcontainers:testcontainers")
	integrationTestImplementation(
			"org.wiremock.integrations.testcontainers:wiremock-testcontainers-module:1.0-alpha-13",
	)
	integrationTestImplementation("org.wiremock:wiremock:3.6.0")
}

tasks.named('test') {
	useJUnitPlatform()
}
